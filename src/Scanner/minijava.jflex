/**
 * JFlex specification for lexical analysis of a simple demo language.
 * Change this into the scanner for your implementation of MiniJava.
 *
 * CSE 401/M501/P501 19au, 20sp, 20au
 */


package Scanner;

import java_cup.runtime.Symbol;
import java_cup.runtime.ComplexSymbolFactory;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import java_cup.runtime.ComplexSymbolFactory.Location;
import Parser.sym;

%%
%public
%final
%class scanner
%unicode
%cup
%line
%column

/* The following code block is copied literally into the generated scanner
 * class. You can use this to define methods and/or declare fields of the
 * scanner class, which the lexical actions may also reference. Most likely,
 * you will only need to tweak what's already provided below.
 *
 * We use CUP's ComplexSymbolFactory and its associated ComplexSymbol class
 * that tracks the source location of each scanned symbol.
 */
%{
  /** The CUP symbol factory, typically shared with parser. */
  private ComplexSymbolFactory symbolFactory = new ComplexSymbolFactory();

  /** Initialize scanner with input stream and a shared symbol factory. */
  public scanner(java.io.Reader in, ComplexSymbolFactory sf) {
    this(in);
    this.symbolFactory = sf;
  }

  /**
   * Construct a symbol with a given lexical token, a given
   * user-controlled datum, and the matched source location.
   *
   * @param code     identifier of the lexical token (i.e., sym.<TOKEN>)
   * @param value    user-controlled datum to associate with this symbol
   * @effects        constructs new ComplexSymbol via this.symbolFactory
   * @return         a fresh symbol storing the above-desribed information
   */
  private Symbol symbol(int code, Object value) {
    // Calculate symbol location
    int yylen = yylength();
    Location left = new Location(yyline + 1, yycolumn + 1);
    Location right = new Location(yyline + 1, yycolumn + yylen);
    // Calculate symbol name
    int max_code = sym.terminalNames.length;
    String name = code < max_code ? sym.terminalNames[code] : "<UNKNOWN(" + yytext() + ")>";
    return this.symbolFactory.newSymbol(name, code, left, right, value);
  }

  /**
   * Construct a symbol with a given lexical token and matched source
   * location, leaving the user-controlled value field uninitialized.
   *
   * @param code     identifier of the lexical token (i.e., sym.<TOKEN>)
   * @effects        constructs new ComplexSymbol via this.symbolFactory
   * @return         a fresh symbol storing the above-desribed information
   */
  private Symbol symbol(int code) {
    // Calculate symbol location
    int yylen = yylength();
    Location left = new Location(yyline + 1, yycolumn + 1);
    Location right = new Location(yyline + 1, yycolumn + yylen);
    // Calculate symbol name
    int max_code = sym.terminalNames.length;
    String name = code < max_code ? sym.terminalNames[code] : "<UNKNOWN(" + yytext() + ")>";
    return this.symbolFactory.newSymbol(name, code, left, right);
  }

  /**
   * Convert the symbol generated by this scanner into a string.
   *
   * This method is useful to include information in the string representation
   * in addition to the plain CUP name for a lexical token.
   *
   * @param symbol   symbol instance generated by this scanner
   * @return         string representation of the symbol
   */
  public String symbolToString(Symbol s) {
    // All symbols generated by this class are ComplexSymbol instances
    ComplexSymbol cs = (ComplexSymbol)s; 
    switch (cs.sym) {
      case sym.PRINT: return "PRINT";

      /* operators */
      case sym.PLUS: return "PLUS";
      case sym.MINUS: return "MINUS";
      case sym.MULT: return "MULT";
      case sym.LESS: return "LESS";
      case sym.AND: return "AND";
      case sym.NOT: return "NOT";
      case sym.BECOMES: return "BECOMES";

      /* delimiters */
      case sym.LPAREN: return "LPAREN";
      case sym.RPAREN: return "RPAREN";
      case sym.LBRACE: return "LBRACE";
      case sym.RBRACE: return "RBRACE";
      case sym.LBRACKET: return "LBRACKET";
      case sym.RBRACKET: return "RBRACKET";
      case sym.SEMICOLON: return "SEMICOLON";
      case sym.COMMA: return "COMMA";

      /* types (Type) */ 
      case sym.BOOLEAN: return "BOOLEAN";
      case sym.INT: return "INT";

      /* truth values (Expression) */
      case sym.TRUE: return "TRUE";
      case sym.FALSE: return "FALSE";

      /* etc. */
      case sym.THIS: return "THIS";
      case sym.NEW: return "NEW";
      case sym.CLASS: return "CLASS";
      case sym.PUBLIC: return "PUBLIC";
      case sym.STATIC: return "STATIC";
      case sym.VOID: return "VOID";
      case sym.MAIN: return "MAIN";
      case sym.STRING: return "STRING";
      case sym.EXTENDS: return "EXTENDS";
      case sym.RETURN: return "RETURN";
      case sym.WHILE: return "WHILE";
      case sym.IF: return "IF";
      case sym.ELSE: return "ELSE";
      case sym.DOT: return "DOT";
      case sym.LENGTH: return "LENGTH";

      /* integers */
      case sym.INTEGER: return "INTEGER(" + (String)cs.value + ")";

      /* identifiers */ 
      case sym.IDENTIFIER: return "ID(" + (String)cs.value + ")";

      /* exceptions */ 
      case sym.error: return "<UNEXPECTED(" + (String)cs.value + ")>";
      default: return cs.getName();
    } 
  }
%}

/* Helper definitions */
letter = [a-zA-Z]
digit = [0-9]
pos_digit = [1-9]
eol = [\r\n]
white = {eol}|[ \t]

%%

/* Token definitions */

/* reserved words (first so that they take precedence over identifiers) */

// TODO: Am I missing any symbols?

// "display" { return symbol(sym.DISPLAY); }
"System.out.println" { return symbol(sym.PRINT); }

/* operators */
"+" { return symbol(sym.PLUS); }
"-" { return symbol(sym.MINUS); }
"*" { return symbol(sym.MULT); }
"<" { return symbol(sym.LESS); }
"&&" { return symbol(sym.AND); }
"!" { return symbol(sym.NOT); }
"=" { return symbol(sym.BECOMES); }

/* delimiters */
"(" { return symbol(sym.LPAREN); }
")" { return symbol(sym.RPAREN); }
"{" { return symbol(sym.LBRACE); }
"}" { return symbol(sym.RBRACE); }
"[" { return symbol(sym.LBRACKET); }
"]" { return symbol(sym.RBRACKET); }
";" { return symbol(sym.SEMICOLON); }
"," { return symbol(sym.COMMA); }

/* types (Type) */ 
"boolean" { return symbol(sym.BOOLEAN); }
"int" { return symbol(sym.INT); }

/* truth values (Expression) */
"true" { return symbol(sym.TRUE); }
"false" { return symbol(sym.FALSE); }

/* etc. */
"this" { return symbol(sym.THIS); }
"new" { return symbol(sym.NEW); }
"class" { return symbol(sym.CLASS); }
"public" { return symbol(sym.PUBLIC); }
"static" { return symbol(sym.STATIC); }
"void" { return symbol(sym.VOID); }
"main" { return symbol(sym.MAIN); }
"String" { return symbol(sym.STRING); }
"extends" { return symbol(sym.EXTENDS); }
"return" { return symbol(sym.RETURN); }
"while" { return symbol(sym.WHILE); }
"if" { return symbol(sym.IF); }
"else" { return symbol(sym.ELSE); }
"." { return symbol(sym.DOT); }
"length" { return symbol(sym.LENGTH); }

/* integers */
{digit} | {pos_digit}{digit}* { return symbol(sym.INTEGER, yytext()); }

/* identifiers */
{letter} ({letter}|{digit}|_)* {
  return symbol(sym.IDENTIFIER, yytext());
}

/* whitespace */
{white}+ { /* ignore whitespace */ }

// /* comment */
"/*" ( [^"/"] | [^"*"]"/" )* "*/"  { /* ignore comments */ }

// /* single line comment */
"//"[^{eol}]*{eol}  { /* ignore comments */ }

/* lexical errors (last so other matches take precedence) */
. {
    System.err.printf(
      "%nUnexpected character '%s' on line %d at column %d of input.%n",
      yytext(), yyline + 1, yycolumn + 1
    );
    return symbol(sym.error, yytext());
  }

<<EOF>> { return symbol(sym.EOF); }
